---
layout: post
title: Perl Weekly Challenge 112.
comments: true
excerpt: Canonical path and Climb stairs.
tags:
   - pwc
   - perl
---

My solutions ([task 1](https://github.com/wlmb/perlweeklychallenge-club/blob/master/challenge-112/wlmb/perl/ch-1.pl), [task 2](https://github.com/wlmb/perlweeklychallenge-club/blob/master/challenge-112/wlmb/perl/ch-2.pl) and [task 2a](https://github.com/wlmb/perlweeklychallenge-club/blob/master/challenge-112/wlmb/perl/ch-2a.pl)) to the  [The Weekly Challenge - 112](https://perlweeklychallenge.org/blog/perl-weekly-challenge-112).


# Task 1: Canonical Path

    Submitted by: Mohammad S Anwar
    You are given a string path, starting with a slash â€˜/'.

    Write a script to convert the given absolute path to the
    simplified canonical path.

    In a Unix-style file system:

    - A period '.' refers to the current directory
    - A double period '..' refers to the directory up a level
    - Multiple consecutive slashes ('//') are treated as a
      single slash '/'
    The canonical path format:

    - The path starts with a single slash '/'.
    - Any two directories are separated by a single slash '/'.
    - The path does not end with a trailing '/'.
    - The path only contains the directories on the path from
      the root directory to the target file or directory
    Example
    Input: "/a/"
    Output: "/a"

    Input: "/a/b//c/"
    Output: "/a/b/c"

    Input: "/a/b/c/../.."
    Output: "/a"

For this task I repeatedly apply substitutions to simplify the path,
until it doesn't change anymore.

    # Perl weekly challenge 112
    # Task 1: Canonical path
    #
    # See https://wlmb.github.io/2021/05/12/PWC112/#task-1-canonical-path
    use strict;
    use warnings;
    use v5.12;
    use Cwd qw(cwd);

    my $cwd=cwd; # Current working directory, for relative paths
    for my $input(@ARGV){ #provide paths in @ARGV
        my $path=$input;
        $path = "$cwd/$path" unless $path=~m{^/}; # relative -> absolute path
        $path.="/"; #add temporal trailing slash as guard
        while($path=~s{//}{/}){}; # remove //
        while($path=~s{/\./}{/}){}; # remove all /.
        while($path=~s{/[^/]+?/\.\./}{/}){}; # remove all /dir/..
        while($path=~s{^/(\.\./)+}{/}){}; # remove all leading  /..
        $path=~s{^(/.*)/$}{$1}; # remove trailing / if not first
        say "Input: $input Output: $path";
    }

Example:

    ./ch-1.pl /a/ /a/b//c/ /a/b/c/../..

Results:

    Input: /a/ Output: /a
    Input: /a/b//c/ Output: /a/b/c
    Input: /a/b/c/../.. Output: /a

More contrived examples: parent of root directory, files with dots,
relative paths.

    ./ch-1.pl /../../.. /.file /file. /..file /file.. ../../../../../../../../file/

Results:

    Input: /../../.. Output: /
    Input: /.file Output: /.file
    Input: /file. Output: /file.
    Input: /..file Output: /..file
    Input: /file.. Output: /file..
    Input: ../../../../../../../../file/ Output: /home/mochan/file


# Task 2: Climb Stairs

    Submitted by: Mohammad S Anwar
    You are given $n steps to climb

    Write a script to find out the distinct ways to climb to the
    top. You are allowed to climb either 1 or 2 steps at a time.

    Example
    Input: $n = 3
    Output: 3

        Option 1: 1 step + 1 step + 1 step
        Option 2: 1 step + 2 steps
        Option 3: 2 steps + 1 step

    Input: $n = 4
    Output: 5

        Option 1: 1 step + 1 step + 1 step + 1 step
        Option 2: 1 step + 1 step + 2 steps
        Option 3: 2 steps + 1 step + 1 step
        Option 4: 1 step + 2 steps + 1 step
        Option 5: 2 steps + 2 steps

Consider choosing `$n1` single steps and `$n2` double steps. Then
`$n==$N1+2*$N2`, and `$N2` can take any value from `0` up
to `$n/2`. For each value of `$N2` I can reorder the steps
arbitrarily, but permuting two single steps or two double steps has no
effect. Thus, the total number of distinct ways to climb is
`factorial($n1+$n2)/(factorial($n1)*factorial($n2)` summed over all
possible values of `$n2`, where `factorial($x)` is the factorial
function that yields the total number of possible orderings of `$x`
objects.

    # Perl weekly challenge 112
    # Task 2: Climb stairs. Count ways.
    #
    # See https://wlmb.github.io/2021/05/12/PWC112/#task-2-climb-stairs
      use strict;
      use warnings;
      use v5.12;
      use Memoize;
      foreach my $n(@ARGV){ # Number of steps from @ARGV
          my $ways=0;
          foreach my $n2(0..$n/2){
    	  my $n1=$n-2*$n2;
    	  $ways+=factorial($n1+$n2)/(factorial($n1)*factorial($n2));
          }
          say "Input: $n  Output: $ways";
      }

For the factorial I use a simple memoized recursive implementation.

    memoize('factorial');
    sub factorial {
        my $x=shift @_; #assume non-negative integer
        return 1 if $x==0 or $x==1;
        return $x*factorial($x-1);
    }

Example:

    ./ch-2.pl `seq 10`

Results:

    Input: 1  Output: 1
    Input: 2  Output: 2
    Input: 3  Output: 3
    Input: 4  Output: 5
    Input: 5  Output: 8
    Input: 6  Output: 13
    Input: 7  Output: 21
    Input: 8  Output: 34
    Input: 9  Output: 55
    Input: 10  Output: 89

One has to beware that the factorial function grows very fast, so the
simple program above may fail due to integer overflow for relatively
small numbers. It could be fixed by using `bigint`, or approximate
solutions could be obtained by replacing the factorials by the
logarithm of the gamma function (available for example in `PDL`), replacing the divisions by
substractions and taking the exponential of the result.

It could be of interest to actually produce all the sequences of
steps. To that end I use a combinator-iterator that yields all
combinations of `$n` bits that has `$k` ones. The iterator is quite
dumb. It looks at all `$n` bit numbers and filters out those that don't
have the correct number of ones. Then I interpret each 1 as a single
and each 0 as a double step.

    # Perl weekly challenge 112
    # Task 2: Climb stairs. List ways.
    #
    # See https://wlmb.github.io/2021/05/12/PWC112/#task-2-climb-stairs
      use strict;
      use warnings;
      use v5.12;
      use List::Util qw(sum0);
      foreach my $n(@ARGV){  # Number of steps from @ARGV
          say "\nInput: $n\nCombinations:";
          foreach my $n2(0..$n/2){
    	  my $n1=$n-2*$n2;
    	  my $total=$n1+$n2;
    	  my $combinator=combinator($total, $n1);
    	  while(my @combination=$combinator->()){
    	      say join ",", map {$_==0?"double":"single"} @combination;
    	  }
          }
      }

      sub combinator { # produces combinations of n taken k at a time
          my ($n,$k)=@_;
          my @number=((1) x $k, (0) x ($n-$k)); # binary $n-bit number as array
          my $done=0;
          my $iter=0;
          sub { #dumb but simple
    	  return if $done;
    	  return @number if $iter++==0; #first time through
    	  while(increment(@number)){
    	      return @number if sum0(@number)==$k;
    	  }
    	  $done=1;
    	  return;
          }
      }

      sub increment {
          $_[0]++; #use @_ directly to modify it
          for(0..@_-2){
    	  return @_ if $_[$_] < 2;
    	  $_[$_]=0;  #carry to next digit
    	  ++$_[$_+1];
          }
          return @_ if $_[-1] < 2;
      }

Example:

    ./ch-2a.pl `seq 7`

Results:


    Input: 1
    Combinations:
    single

    Input: 2
    Combinations:
    single,single
    double

    Input: 3
    Combinations:
    single,single,single
    single,double
    double,single

    Input: 4
    Combinations:
    single,single,single,single
    single,single,double
    single,double,single
    double,single,single
    double,double

    Input: 5
    Combinations:
    single,single,single,single,single
    single,single,single,double
    single,single,double,single
    single,double,single,single
    double,single,single,single
    single,double,double
    double,single,double
    double,double,single

    Input: 6
    Combinations:
    single,single,single,single,single,single
    single,single,single,single,double
    single,single,single,double,single
    single,single,double,single,single
    single,double,single,single,single
    double,single,single,single,single
    single,single,double,double
    single,double,single,double
    double,single,single,double
    single,double,double,single
    double,single,double,single
    double,double,single,single
    double,double,double

    Input: 7
    Combinations:
    single,single,single,single,single,single,single
    single,single,single,single,single,double
    single,single,single,single,double,single
    single,single,single,double,single,single
    single,single,double,single,single,single
    single,double,single,single,single,single
    double,single,single,single,single,single
    single,single,single,double,double
    single,single,double,single,double
    single,double,single,single,double
    double,single,single,single,double
    single,single,double,double,single
    single,double,single,double,single
    double,single,single,double,single
    single,double,double,single,single
    double,single,double,single,single
    double,double,single,single,single
    single,double,double,double
    double,single,double,double
    double,double,single,double
    double,double,double,single
